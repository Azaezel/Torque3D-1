function UIBuilder::TextField(%labelText, %variableName)
{
   if(UIBuilderStack.count() == 0)
   {
      error("UIBuilder::TextField() - attempting to create element, but we have no containing control to add it to!");
      return false;
   }
   
   %baseCtrl = UIBuilderStack.getKey(UIBuilderStack.count()-1);
   
   %container = new GuiControl()
   {
      position = 0 SPC 0;
      extent = %baseCtrl.extent.x SPC $UIBuilder::rowHeight;
      internalName = "textFieldContainer";
      horizSizing = "right";
      vertSizing = "bottom";
   };
   
   %label = new GuiTextCtrl()
   {
      profile = ToolsGuiTextProfile;
      position = "0 0";
      extent = (%baseCtrl.extent.x / 2) SPC $UIBuilder::rowHeight;
      text = %labelText;
      internalName = "textFieldLabel";
      horizSizing = "width";
      vertSizing = "bottom";
   };
   %container.add(%label);
   
   %defaultValue = getVariable(%variableName);
   
   %edit = new GuiTextEditCtrl()
   {
      profile = ToolsGuiTextEditProfile;
      position = (%baseCtrl.extent.x / 2) SPC 0;
      extent = (%baseCtrl.extent.x / 2) SPC $UIBuilder::rowHeight; 
      text = %defaultValue;
      variable = %variableName;
      internalName = "textFieldEdit";
      horizSizing = "width";
      vertSizing = "bottom";
   };
   %container.add(%edit);
   
   if(%baseCtrl.getClassName() $= "GuiStackControl" && %baseCtrl.StackingType $= "Horizontal")
   {
      %container.horizSizing = "right";
      %container.vertSizing = "height";
   }
   
   %baseCtrl.add(%container);
   
   return %container;
}

function GuiFormBuilder::createTextPropertyField(%base, %labelText, %target, %accessor)
{
   %targetType = "";
   
   if(isObject(%target) && %accessor !$= "")
   {
      if(%target.getClass() $= "Settings")
      {
         %targetType = "settings";
         %defaultValue = %target.value(%accessor); 
      }
      else
      {
         %targetType = "object";
         %defaultValue = %target.getField(%accessor); 
      }
   }
   else if(isDefined(%target))
   {
      %targetType = "variable";
      %getValueCommand = "$result = " @ %target @ ";";
      eval(%getValueCommand);
      %defaultValue = $result;  
   }
   
   %fieldContainer = %base.createTextField(%labelText, %defaultValue);
   %fieldContainer-->textFieldEdit.superClass = GuiFormBuilderPropertyField; //this integrates in callbacks so changes to the field apply changes to the target
   %fieldContainer-->textFieldEdit.target = %target;
   %fieldContainer-->textFieldEdit.targetType = %targetType;
   %fieldContainer-->textFieldEdit.targetAccessor = %accessor;
   
   return %fieldContainer;
}