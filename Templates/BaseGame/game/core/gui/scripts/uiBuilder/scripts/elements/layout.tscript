function UIBuilder::LayoutContainer(%pos, %ext)
{
   %container = new GuiContainer()
   {
      profile = ToolsGuiFrameSetProfile;
      class = UIBuilderLayout;
      position = %pos;
      extent = %ext;
      internalName = "layout";
      horizSizing = "width";
      vertSizing = "height";
   };

   UIBuilderStack.push_back(%container);
   
   %layoutTabbook = UIBuilder::TabBook(true, true);
   %layoutTabbook.class = UIBldrLayoutTabbook;
   %layoutTabbook.internalName = "LayoutTabbook";
   %layoutTabbook.allowReorder = true;
   
   //%baseCtrl.add(%container);
   
   return %container;
}

function UIBuilderLayout::undockContent(%this, %control)
{
   
}

function UIBuilder::makeLayoutDragNDropLayer()
{
   if(!isObject(EditorDragAndDropLayer))
   {
      new GuiControl(EditorDragAndDropLayer)
      {
         position = "0 0";
         extent = Canvas.extent;
      };
      
      EditorDragAndDropLayer.highlightControl = new GuiPanel() {
          position = "0 0";
          extent = "20 20";
          profile = UIBuilderLayoutHighlightProfile; // Assume this is a predefined profile for highlighting.
          visible = false;
      };
      
      EditorDragAndDropLayer.add(EditorDragAndDropLayer.highlightControl);
   }  
}
//==============================================================================
function UIBldrLayoutTabbook::onTabDraggedOut(%this, %tabObj)
{
   echo("UIBldrLayoutTabbook::onTabDraggedOut() - tab: " @ %tabObj);
   
   %tabText = %tabObj.text;
   %tabTextWidth = %tabObj.profile.getStringWidth(%tabText) + 30;
   
   UIBuilder::makeLayoutDragNDropLayer();
   
   %cursorpos = Canvas.getCursorPos();
   %xPos = getWord( %cursorpos, 0 );
   %yPos = getWord( %cursorpos, 1 );
   
   %tabRef = new GuiTextCtrl() {
      profile = GuiMenuTextProfile;
      text = %tabText;
      extent = %tabTextWidth SPC 30;
      tabRef = %tabObj;
   };
   
   // Create the drag control.
   %ctrl = new GuiDragAndDropControl()
   {
      canSaveDynamicFields    = "0";
      Profile                 = GuiMenuBackgroundProfile;
      HorizSizing             = "right";
      VertSizing              = "bottom";
      Position                = %xPos SPC %yPos;
      extent                  = %tabRef.extent;
      MinExtent               = "4 4";
      canSave                 = "1";
      Visible                 = "1";
      hovertime               = "1000";

      // Let the GuiDragAndDropControl delete itself on mouse-up.  When the drag is aborted,
      // this not only deletes the drag control but also our payload.
      deleteOnMouseUp         = true;
      
      useWholeCanvas = true;

      // To differentiate drags, use the namespace hierarchy to classify them.
      // This will allow a color swatch drag to tell itself apart from a file drag, for example.
      class                   = "UIBuilderLayoutTabDrop";
      
      tabRefCtrl = %tabRef;
   };
   
   // Add the temporary color swatch to the drag control as the payload.
   %ctrl.add( %tabRef );
   
   // Start drag by adding the drag control to the canvas and then calling startDragging().
   //Canvas.getContent().add( %ctrl );
   EditorDragAndDropLayer.add(%ctrl);
   Canvas.pushDialog(EditorDragAndDropLayer);
   
   %ctrl.startDragging( 0, 0);
   
   Canvas.repaint();
}

function UIBuilderLayoutTabDrop::onControlDragCancelled(%this)
{
   Canvas.popDialog(EditorDragAndDropLayer);
   %this.tabRefCtrl.delete();
}

function UIBldrLayoutTabbook::onControlDragged(%this, %payload, %point)
{
   EditorDragAndDropLayer.highlightControl.position = %this.position;
   EditorDragAndDropLayer.highlightControl.extent = %this.extent.x SPC 20;
   EditorDragAndDropLayer.highlightControl.visible = true;
   
   return;
}

function UIBldrLayoutTabbook::onControlDropped(%this, %payload, %point)
{
   Canvas.popDialog(EditorDragAndDropLayer);
   
   if(isObject(%payload.tabRef))
      %this.dock(%payload.tabRef, "tab");
   else
      %this.dock(%payload, "tab");
}

function UIBldrLayoutTab::onControlDragged(%this, %payload, %point)
{
   %targetPos = %this.getGlobalPosition();
   
   echo("targetObj: " @ %this @ " targetPos: " @ %targetPos);

   %targetX = getWord(%targetPos, 0);
   %targetY = getWord(%targetPos, 1);
   %targetWidth = %this.extent.x;
   %targetHeight = %this.extent.y;

   // Calculate the 20% margin for each side.
   %marginWidth = %targetWidth * 0.2;
   %marginHeight = %targetHeight * 0.2;
   
   %mouseX = %point.x;
   %mouseY = %point.y;

   // Check if the mouse is within the target's bounds.
   if (%mouseX >= %targetX && %mouseX <= (%targetX + %targetWidth) &&
      %mouseY >= %targetY && %mouseY <= (%targetY + %targetHeight)) {
      // Determine the mouse position relative to the target's edges.
      %relativeX = %mouseX - %targetX;
      %relativeY = %mouseY - %targetY;

      // Initialize highlight position and size variables.
      %highlightX = %targetX;
      %highlightY = %targetY;
      %highlightWidth = %targetWidth;
      %highlightHeight = %targetHeight/2; // Default to top section height.

      // Check if within top 20% margin.
      if (%relativeY <= %marginHeight) {
          // Highlight the top area.
          %highlightY = %targetY;
          %payload.targetPosition = "top";
      }
      // Check if within bottom 20% margin.
      else if (%relativeY >= (%targetHeight - %marginHeight)) {
          // Highlight the bottom area.
          %highlightY = %targetY + (%targetHeight/2);
          %payload.targetPosition = "bottom";
      }
      // Check if within left 20% margin.
      else if (%relativeX <= %marginWidth) {
          // Highlight the left area, adjust width and height for vertical highlight.
          %highlightX = %targetX;
          %highlightWidth = %targetWidth/2;
          %highlightHeight = %targetHeight;
          %payload.targetPosition = "left";
      }
      // Check if within right 20% margin.
      else if (%relativeX >= (%targetWidth - %marginWidth)) {
          // Highlight the right area, adjust width and height for vertical highlight.
          %highlightX = %targetX + (%targetWidth/2);
          %highlightWidth = %targetWidth/2;
          %highlightHeight = %targetHeight;
          %payload.targetPosition = "right";
      }
      else {
          //get the parent tabbook to highlight it'll be converted to a tab
          %tabbook = %this.getParent();
          
          %tabbookPos = %tabbook.getGlobalPosition();
          
          %highlightX = %tabbookPos.x;
          %highlightY = %tabbookPos.y;
          
          %highlightWidth = %tabbook.extent.x;
          %highlightHeight = 20;
          %payload.targetPosition = "tab";
      }
      
      echo("target region: " @ %payload.targetPosition);

      EditorDragAndDropLayer.highlightControl.position = %highlightX SPC %highlightY;
      EditorDragAndDropLayer.highlightControl.extent = %highlightWidth SPC %highlightHeight;
      EditorDragAndDropLayer.highlightControl.visible = true;
   }
   else
   {
      EditorDragAndDropLayer.highlightControl.visible = false;
   }
}

function UIBldrLayoutTab::onControlDropped(%this, %payload, %point)
{
   Canvas.popDialog(EditorDragAndDropLayer);
   
   %tabbook = %this.getParent();
   
   if(isObject(%payload.tabRef))
      %tabbook.dock(%payload.tabRef, %payload.targetPosition);
   else
      %tabbook.dock(%payload, %payload.targetPosition);
}

function UIBldrLayoutTabbook::dock(%this, %dockObject, %position)
{
   %layout = %this.getParent();
   
   if(%dockObject.getClassName() $= "GuiTabPageCtrl")
   {
      %tabObj = %dockObject;
      %tabOldParent = %tabObj.getParent();
   }
   else if(%dockObject.getClassName() $= "GuiWindowCtrl")
   {
      %tabObj = new GuiTabPageCtrl()
      {
         text = %dockObject.text;
         profile = ToolsGuiEditorTabPage;
         position = "0 20";
         extent = %dockObject.extent;
         internalName = "page";  
         horizSizing = "width";
         vertSizing = "height";
         fitBook = true;
         margin = $UIBuilder::edgePad SPC $UIBuilder::edgePad SPC $UIBuilder::edgePad SPC $UIBuilder::edgePad;
      };
      
      for(%i=0; %i < %dockObject.getCount(); %i++)
      {
         %childObj = %dockObject.getObject(%i);
         %tabObj.add(%childObj);
      }
      
      %tabOldParent = %dockObject;
   }
   
   if(%position !$= "tab")
   {
      UIBuilder::Set(%layout);
         %orientation = (%position $= "top" || %position $= "bottom") ? "Horizontal" : "Vertical";
         %splitPoint = %layout.extent.x/2 SPC %layout.extent.y/2;
         
         %split = UIBuilder::SplitContainer(%orientation, %splitPoint);
         UIBuilder::End();
      UIBuilder::End();
         
      %moveCurrentToPanel = "";
      %newBookPanel = "";
      
      switch$(%position)
      {
         case "left":
            %newBookPanel = %split-->Panel1;
            %moveCurrentToPanel = %split-->Panel2;
         case "right":
            %newBookPanel = %split-->Panel2;
            %moveCurrentToPanel = %split-->Panel1;
         case "top":
            %newBookPanel = %split-->Panel1;
            %moveCurrentToPanel = %split-->Panel2;
         case "bottom":
            %newBookPanel = %split-->Panel2;
            %moveCurrentToPanel = %split-->Panel1;
         default:
            error("UIBldrLayoutTabbook::dock() - invalid drop region");
      }
      
      UIBuilder::Set(%newBookPanel);
         %newBook = UIBuilder::TabBook(true, true);
         %newBook.class = UIBldrLayoutTabbook;
         %newBook.add(%tabObj);
         
         //%newBook.resize(0,0,%newBookPanel.extent.x,%newBookPanel.extent.y);
         %tabObj.resize(0,0,%newBook.extent.x,%newBook.extent.y);
         UIBuilder::End();
      UIBuilder::End();
      
      %moveCurrentToPanel.add(%this);
      if(isObject(%this.actionButton))
      {
         %moveCurrentToPanel.add(%this.actionButton);
         %this.actionButton.position.x = %moveCurrentToPanel.extent.x - 20;  
      }
      %panelExt = %moveCurrentToPanel.extent;
      %this.resize(0,0,%panelExt.x,%panelExt.y);
      
      //force compliance by re-setting the split point just to be sure
      %split.splitPoint = %splitPoint;
   }
   else
   {
      %this.add(%tabObj);
   }
   
   if(%tabOldParent.getClassName() $= "GuiTabbookCtrl")
      %tabOldParent.undock(%tabObj);
   else if(%tabOldParent.getClassName() $= "GuiWindowCtrl")
      %tabOldParent.delete();
}

function UIBldrLayoutTabbook::undock(%this, %tabObj)
{
   //Now we update the original holder of our moved tab, and adjust layout stuffs as needed
   if(%this.getCount() == 0)
   {
      //we need to collapse it  
      %oldPanel = %this.getParent();
      %oldSplitCtrl = %oldPanel.getParent();
      
      if(%oldSplitCtrl.getClassName() $= "GuiSplitContainer")
      {
         //we need to fold things up  
         %splitContainerParent = %oldSplitCtrl.getParent();
         
         //get our sibling
         %objIdx = %oldSplitCtrl.getObjectIndex(%oldPanel);
         
         %siblingIndex = %objIdx == 0 ? 1 : 0;
         
         %otherPanel = %oldSplitCtrl.getObject(%siblingIndex);
         
         %otherTabbook = %otherPanel.getObject(0);
         %splitContainerParent.add(%otherTabbook);
         %otherTabbook.resize(0,0,%splitContainerParent.extent.x, %splitContainerParent.extent.y);
         %oldSplitCtrl.delete();
         %otherPanel.delete();
         %this.schedule(32, "delete");
      }
      else
      {
         %oldPanel.delete();  
      }
   }
}

function UIBldrLayoutTabbook::onTabAction(%this)
{
   if(!isObject(TabBookActionPopup))
   {
      new PopupMenu( TabBookActionPopup )
      {
         superClass = "MenuBuilder";
         class = "EditorWorldMenu";
         //isPopup = true;

         item[ 0 ] = "Undock Tab" TAB "" TAB "performLayoutUndock();";
      };
   }
   
   TabBookActionPopup.showPopup(Canvas);
   TabBookActionPopup.tabbookId = %this;
}

function performLayoutUndock()
{
   %tabIdx = TabBookActionPopup.tabbookId.getSelectedPage();
   %tabObj = TabBookActionPopup.tabbookId.getObject(%tabIdx);

   //now that we've undocked it, make a new window and shove the tab into it
   %newWindowPos = TabBookActionPopup.tabbookId.getGlobalPosition();
   %newWindowPos.x += 20;
   %newWindowPos.y += 20;
   
   %newWindowExt = TabBookActionPopup.tabbookId.extent;
   %newWindowExt.x -= 40;
   %newWindowExt.y -= 40;
   %newWindow = UIBuilder::Window(%tabObj.text, %newWindowPos, %newWindowExt, true);
   UIBuilder::End();
   
   for(%i=0; %i < %tabObj.getCount(); %i++)
   {
      %childObj = %tabObj.getObject(%i);
      %newWindow.add(%childObj);
   }
   
   Canvas.getContent().add(%newWindow);
   
   TabBookActionPopup.tabbookId.remove(%tabObj);
   %tabObj.delete();
   
   TabBookActionPopup.tabbookId.undock(%tabObj);
}