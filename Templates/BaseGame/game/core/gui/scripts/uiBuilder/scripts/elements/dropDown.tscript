function UIBuilder::Dropdown(%list, %variableName, %changeCommand)
{
   if(UIBuilderStack.count() == 0)
   {
      error("UIBuilder::Dropdown() - attempting to create element, but we have no containing control to add it to!");
      return false;
   }
   
   %baseCtrl = UIBuilderStack.getKey(UIBuilderStack.count()-1);
   
   %defaultValue = getVariable(%variableName);
   
   %edit = new GuiPopUpMenuCtrlEx()
   {
      class = UIBuilderDropdown;
      profile = ToolsGuiPopUpMenuProfile;
      extent = %baseCtrl.extent.x SPC $UIBuilder::rowHeight; 
      text = %defaultValue;
      variable = %variableName;
      command = %changeCommand;
      internalName = "dropdown";
      horizSizing = "width";
      vertSizing = "bottom";
   };
   
   if(%baseCtrl.getClassName() $= "GuiStackControl" && %baseCtrl.StackingType $= "Horizontal")
   {
      %edit.horizSizing = "right";
      %edit.vertSizing = "height";
   }

   %baseCtrl.add(%edit);
   
   for(%i=0; %i < getFieldCount(%list); %i++)
   {
      %entry = getField(%list, %i);
      %edit.add(%entry);  
   }
   
   return %edit;
}

function UIBuilderDropdown::setCanSearch(%this, %searchable)
{
   %this.allowTextSearch = %searchable;
}
