function mazeGen::getGreebleList(%this)
{
    if (isObject(GreebleList)) return;
    
    new arrayobject(GreebleList);
    
    new arrayobject(floor);
        floor.add("TSStatic", "mazeGen:barrel_shape");
        floor.add("TSStatic", "mazeGen:crateA_shape");
        floor.add("TSStatic", "mazeGen:crateB_shape");
        floor.add("TSStatic", "mazeGen:crateC_shape");
        floor.add("TSStatic", "mazeGen:crateD_shape");
        floor.add("TSStatic", "mazeGen:crateE_shape");
        floor.add("TSStatic", "mazeGen:crateF_shape");
        floor.add("TSStatic", "mazeGen:crateG_shape");
        floor.add("TSStatic", "mazeGen:crateH_shape");
        floor.add("TSStatic", "mazeGen:crateI_shape");
        floor.add("TSStatic", "mazeGen:crateJ_shape");
        floor.add("TSStatic", "mazeGen:crateK_shape");
        floor.add("TSStatic", "mazeGen:crateL_shape");
        floor.add("Prefab", "data/trapsNTriggers/prefabs/electricHazzard.prefab");
        floor.add("StaticShape", "SpikeTrap");
    GreebleList.add(0,floor);
    
    new arrayobject(floorMid);
        floorMid.add("TSStatic", "mazeGen:carRamp_shape");
        floorMid.add("Prefab", "data/trapsNTriggers/prefabs/electricHazzard.prefab");
        floorMid.add("StaticShape", "SpikeTrap");
    GreebleList.add(1,floorMid);
    
    new arrayobject(floorBig);
        floorBig.add("TSStatic", "mazeGen:overpassA_shape");
        floorBig.add("TSStatic", "mazeGen:overpassB_shape");
        floorBig.add("TSStatic", "mazeGen:overpassC_shape");
        floorBig.add("Prefab", "data/trapsNTriggers/prefabs/electricHazzard.prefab");
        floorBig.add("StaticShape", "SpikeTrap");
    GreebleList.add(2,floorBig);    
    
    new arrayobject(wallL);
        wallL.add("TSStatic", "mazeGen:jetRampL_shape");
        wallL.add("Prefab", "data/trapsNTriggers/prefabs/electricHazzard.prefab");
    GreebleList.add(3,wallL);
    
    new arrayobject(wallR);
        wallR.add("TSStatic", "mazeGen:jetRampR_shape");
        wallR.add("ParticleEmitterNode", "steamEM");
        wallR.add("Prefab", "data/trapsNTriggers/prefabs/electricHazzard.prefab");
    GreebleList.add(4,wallR);
    
    new arrayobject(ceiling);
        ceiling.add("TSStatic", "mazeGen:jetRampD_shape");
        ceiling.add("Prefab", "data/trapsNTriggers/prefabs/electricHazzard.prefab");
    GreebleList.add(5,ceiling);
    
    new arrayobject(ceilingR);
        ceilingR.add("TSStatic", "mazeGen:jetRampR_shape");
        ceilingR.add("Prefab", "data/trapsNTriggers/prefabs/electricHazzard.prefab");
    GreebleList.add(6,ceilingR);
    
    new arrayobject(ceilingL);
        ceilingL.add("TSStatic", "mazeGen:jetRampL_shape");
        ceilingL.add("Prefab", "data/trapsNTriggers/prefabs/electricHazzard.prefab");
    GreebleList.add(7,ceilingL);
}

function mazeGen::fillGreebles(%this,%nodeObj)
{
    if (!isObject(Greebles))
    {
        new SimGroup(Greebles){};
        getScene(0).add(Greebles);
    }
    %frameMesh = %nodeObj.getChildGroup()-->frame;
    
    %i=0;
    while(%i< GreebleList.count())
    {        
        %grebCount=0;
        %grebType = GreebleList.getValue(%i).getName();
        while (%frameMesh.hasNode(%grebType @ %grebCount+1))
        {
            %spawnTrans = %frameMesh.getNodeTransform(%grebType @ %grebCount+1);
            %pos = getWords(%spawnTrans,0,2);             
            %rot  = getWords(%spawnTrans,3,5) SPC mRound(mRadToDeg(getWord(%spawnTrans,6)));
            %grebbleEntry = getRandom(%grebType.count()-1);
            %greebleClass = %grebType.getKey(%grebbleEntry);
            %greebleMeta = %grebType.getValue(%grebbleEntry);
            if (getRandom()<=%this.greebleFrequency)
            {
                if (%greebleClass $= "TSStatic")
                {
                    %nuGreeble = new TSStatic() {
                        ShapeAsset = %greebleMeta;
                        collisionType = "Visible Mesh";
                        decalType = "Visible Mesh";
                        position = %pos;
                        rotation = %rot;
                    };
                }
                else if (%greebleClass $= "ParticleEmitterNode")
                {
                    %nuGreeble = new ParticleEmitterNode() {
                        emitter = %greebleMeta;
                        datablock = DefaultEmitterNodeData;
                        position = %pos;
                        rotation = %rot;
                    };
                
                }
                else if (%greebleClass $= "Prefab")
                {
                    %nuGreeble = new Prefab() {
                        fileName = %greebleMeta;
                        position = %pos;
                        rotation = %rot;
                    };
                
                }
                else if (%greebleClass $= "StaticShape")
                {
                    %nuGreeble = new StaticShape() {
                        datablock = %greebleMeta;
                        position = %pos;
                        rotation = %rot;
                    };
                
                }
                Greebles.add(%nuGreeble);
            }
            %grebCount++;
        }
        %i++;
    }
}